grammar org.myasm.assembly.compiler.MyAsm hidden(WS, DOC_COMMENT, SL_COMMENT)

generate myAsm "http://www.myasm.org/assembly/compiler/MyAsm"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

// ===============================================
// ===              Java Program               ===
// ===============================================

Program:
	program=CompilationUnit
;

CompilationUnit:
    package=PackageDeclaration? imports+=ImportDeclaration* declarations+=TypeDeclaration*
;

// ===============================================
// ===               Declarations              ===
// ===============================================

PackageDeclaration:
    => PACKAGE name=PackageName ";"
;

ImportDeclaration:
    => IMPORT ( package=PackageName ".*" | object=TypeName ) ";"
;

TypeDeclaration:
    ClassDeclaration | InterfaceDeclaration
;

ClassDeclaration:
    modifiers+=ClassModifier*
    CLASS name=IDENTIFIER extends=SuperClass? implements=Interfaces? body=ClassBody
;

ClassModifier returns Modifier:
    PUBLIC | ABSTRACT | FINAL
;

SuperClass:
    EXTENDS name=ClassType
;

Interfaces returns InterfaceList:
    keyword=IMPLEMENTS interfaces+=InterfaceType (=> "," interfaces+=IntegralType )*
;

ClassBody returns BodyCode:
    "{" declarations+=ClassBodyDeclaration* "}"
;

ClassBodyDeclaration:
    ClassMemberDeclaration | StaticInitializer | ConstructorDeclaration
;

ClassMemberDeclaration:
    FieldDeclaration | MethodDeclaration
;

StaticInitializer:
    STATIC code=Block
;

ConstructorDeclaration:
    modifiers+=ConstructorModifier*
    header=ConstructorDeclarator throws=Exceptions? body=ConstructorBody
;

ConstructorModifier returns Modifier:
    PUBLIC | PROTECTED | PRIVATE
;

ConstructorDeclarator:
    name=SimpleTypeName
    "(" ( params+=FormalParameter (=> "," params+=FormalParameter )* )? ")"
;

FormalParameter:
    type=Type variable=VariableDeclaratorId
;

Exceptions:
    THROWS exceptions+=ClassType (=> "," exceptions+=ClassType )*
;

ConstructorBody returns BodyCode:
    "{" invocation=ExplicitConstructorInvocation? declarations+=BlockStatement* "}"
;

ExplicitConstructorInvocation:
    ( keyword=THIS "(" args=ArgumentList? ")" ) | ( keyword=SUPER "(" args=ArgumentList? ")" )
;

FieldDeclaration returns Variable:
    modifiers+=FieldModifier*
    type=Type declarations+=VariableDeclarator (=> "," declarations+=VariableDeclarator )* ";"
;

FieldModifier returns Modifier:
    PUBLIC | PROTECTED | PRIVATE | STATIC | FINAL | TRANSIENT | VOLATILE
;

VariableDeclarator:
    facade=VariableDeclaratorId
    ( "=" definition=VariableInitializer )?
;

VariableDeclaratorId:
    name=IDENTIFIER layers+=( "[]" )*
;

VariableInitializer:
    Expression | ArrayInitializer
;

MethodDeclaration returns Method:
    signature=MethodHeader ( body=Block | ";" )
;

MethodHeader:
    modifiers+=MethodModifier*
    returnType=ResultType header=MethodDeclarator throws=Exceptions?
;

ResultType returns Type:
    Type | VOID
;

MethodModifier returns Modifier:
    PUBLIC | PROTECTED | PRIVATE | STATIC | ABSTRACT | FINAL | SYNCHRONIZED | NATIVE
;

MethodDeclarator:
    name=IDENTIFIER
    "(" ( params+=FormalParameter (=> "," params+=FormalParameter)* )? ")"
;

InterfaceDeclaration:
    modifiers+=InterfaceModifier*
    INTERFACE name=IDENTIFIER extends=ExtendsInterfaces? body=InterfaceBody
;

InterfaceModifier returns Modifier:
    PUBLIC | ABSTRACT
;

ExtendsInterfaces returns InterfaceList:
    keyword=EXTENDS interfaces+=InterfaceType ( "," interfaces+=InterfaceType )*
;

InterfaceBody returns BodyCode:
    "{" declarations+=InterfaceMemberDeclaration* "}"
;

InterfaceMemberDeclaration:
    ConstantDeclaration | AbstractMethodDeclaration
;

ConstantDeclaration returns Variable:
    modifiers+=ConstantModifiers* type=Type declarations+=VariableDeclarator
;

ConstantModifiers returns Modifier:
    PUBLIC | STATIC | FINAL
;

AbstractMethodDeclaration returns Method:
    modifiers+=AbstractMethodModifier*
    returnType=ResultType header=MethodDeclarator throws=Exceptions? ";"
;

AbstractMethodModifier returns Modifier:
    PUBLIC | ABSTRACT
;

ArrayInitializer:
    "{" ( definitions+=VariableInitializer (=> "," definitions+=VariableInitializer )* )? ","? "}"
;

// ===============================================
// ===                  Types                  ===
// ===============================================

Type returns Type:
    ( PrimitiveType | ReferenceType ) ArrayType*
;

PrimitiveType:
    NumericType | BOOLEAN
;

NumericType:
    IntegralType | FloatingType
;

IntegralType:
    BYTE | SHORT | INT | LONG | CHAR
;

FloatingType:
    FLOAT | DOUBLE
;

ReferenceType:
    TypeName
;

ClassType:
    TypeName
;

InterfaceType:
    TypeName
;

ArrayType:
    "[]"
;

// ===============================================
// ===          Blocks and Commands            ===
// ===============================================

Block:
    "{" declarations+=BlockStatement* "}"
;

BlockStatement:
    ( LocalVariableDeclaration ";" ) | Statement
;

LocalVariableDeclaration returns Variable:
    type=Type declarations+=VariableDeclarator (=> "," declarations+=VariableDeclarator )*
;

Statement:
    EmptyStatement
    | LabeledStatement
    | IfStatement
    | WhileStatement
    | ForStatement
    | Block
    | ( StatementExpression ";" )
    | SwitchStatement
    | DoStatement
    | BreakStatement
    | ContinueStatement
    | ReturnStatement
    | SynchronizedStatement
    | ThrowsStatement
    | TryStatement
;

EmptyStatement:
    ";"
;

LabeledStatement:
    label=IDENTIFIER ":" statement=Statement
;

StatementExpression:
     => Assignment
    | PreIncrementExpression
    | PreDecrementExpression
    |=> PostfixExpression
    |=> MethodInvocation
    |=> ClassInstanceCreationExpression
;

IfStatement:
    IF "(" condition?=Expression ")" then=Statement
    (=> ELSE else=Statement )?
;

SwitchStatement:
    SWITCH "(" expression=Expression ")" "{"
    ( ( CASE   constants+=Expression ":" )
    | ( DEFAULT ":" )
    | statements+=BlockStatement )* "}"
;

WhileStatement:
    WHILE "(" condition?=Expression ")" statement=Statement
;

DoStatement:
    DO statement=Statement WHILE "(" condition?=Expression ")" ";"
;

ForStatement:
    FOR "(" initExpr=ForInit? ";" condition?=Expression? ";" updateExpr=ForUpdate? ")"
    statement=Statement
;

ForInit   returns ForExpression:
    ( expressions +=StatementExpression (=> "," expressions+=StatementExpression )* )
    | LocalVariableDeclaration
;

ForUpdate returns ForExpression:
    ( expressions +=StatementExpression (=> "," expressions+=StatementExpression )* )
;

BreakStatement:
    BREAK    reference=IDENTIFIER? ";"
;

ContinueStatement:
    CONTINUE reference=IDENTIFIER? ";"
;

ReturnStatement:
    RETURN expression=Expression? ";"
;

ThrowsStatement:
    THROW  expression=Expression  ";"
;

SynchronizedStatement:
    SYNCHRONIZED "(" expression=Expression ")" body=Block
;

TryStatement:
    TRY tryBody=Block
    (=> CATCH "(" params+=FormalParameter ")" catchesBody+=Block )*
    (=> FINALLY finallyBody=Block )?
;

// ===============================================
// ===                 Expression              ===
// ===============================================

Expression:
    => ConditionalExpression | Assignment
;

Assignment:
    object=LeftHandSide operator=AssignmentOperator expression=Expression
;

LeftHandSide:
      => ExpressionName | => FieldAccess | => ArrayAccess
;

AssignmentOperator:
    "=" | "*=" | "/=" | "%=" | "+=" | "-=" | "<<=" | ">>=" | ">>>=" | "&=" | "^=" | "|="
;

ConditionalExpression:
    expression=ConditionalOrExpression
    (=> "?" then=Expression ":" else=ConditionalExpression )?
;

ConditionalOrExpression:
    expressions+=ConditionalAndExpression
    (=> operators+="||" expressions+=ConditionalAndExpression )*
;

ConditionalAndExpression:
    expressions+=InclusiveOrExpression
    (=> operators+="&&" expressions+=InclusiveOrExpression )*
;

InclusiveOrExpression:
    expressions+=ExclusiveOrExpression
    (=> operators+="|" expressions+=ExclusiveOrExpression )*
;

ExclusiveOrExpression:
    expressions+=AndExpression
    (=> operators+="^" expressions+=AndExpression )*
;

AndExpression:
    expressions+=EqualityExpression
    (=> operators+="&" expressions+=EqualityExpression )*
;

EqualityExpression:
    expressions+=RelationalExpression
    (=> operators+=( "==" | "!=" ) expressions+=RelationalExpression )*
;

RelationalExpression:
    expressions+=ShiftExpression
    ( => ( operators+=( "<" | ">" | "<=" | ">=" ) expressions+=ShiftExpression )
    | => ( operators+=INSTANCEOF classes+=ReferenceType ) )*
;

ShiftExpression:
    expressions+=AdditiveExpression
    (=> operator+=( "<<" | ">>" | ">>>" ) expressions+=AdditiveExpression )*
;

AdditiveExpression:
    expressions+=MultiplicativeExpression
    (=> operators+=( "+" | "-" ) expressions+=MultiplicativeExpression )*
;

MultiplicativeExpression:
    expressions+=NoArrayExpression
    (=> operators+=( "*" | "/" | "%" ) expressions+=NoArrayExpression )*
;

CastExpression:
     => "(" type=PrimitiveType ")" expression=NoArrayExpression
    |=> "(" type=ReferenceType ")" expression=NoArrayExpressionWithoutMinus
;

NoArrayExpression:
      PreIncrementExpression
    | PreDecrementExpression
    | SignedExpression
    | NoArrayExpressionWithoutMinus
;

PreDecrementExpression returns PrefixExpression:
    operator="--" expression=NoArrayExpression
;

PreIncrementExpression returns PrefixExpression:
    operator="++" expression=NoArrayExpression
;

SignedExpression:
    operator=( "+" | "-" ) expression=NoArrayExpression
;

NoArrayExpressionWithoutMinus:
    => PostfixExpression
    | operator="~" expression=NoArrayExpression
    | operator="!" expression=NoArrayExpression
    | CastExpression
;

PostfixExpression:
    object=Primary operator=( "--" | "++" )?
//    ( => Primary | ExpressionName ) ( "--" | "++" )
;

MethodInvocation:
        ( method=MethodName "(" args=ArgumentList? ")" )
      | ( => object=Primary "." method=IDENTIFIER "(" args=ArgumentList? ")" )
//    | => ( Primary "." IDENTIFIER "(" ArgumentList? ")" )
//    | => ( SUPER   "." IDENTIFIER "(" ArgumentList? ")" )
;

FieldAccess:
    object=Primary "." field=IDENTIFIER
//    ( Primary "." IDENTIFIER ) | => ( SUPER "." IDENTIFIER )
;

Primary:
    => PrimaryNoNewArray | PrimaryNewArray
;

PrimaryNoNewArray:
    ( literal=Literal
    | keyword=THIS
    | ( "(" expression=Expression ")" )
    | class  =ClassInstanceCreationExpression
    | keyword=SUPER
    | ( keyword=MethodName "(" args=ArgumentList? ")" )
    | ( keyword=ExpressionName dimention=DimentionsExpression ) )
    extension=ExpressionExtends
//    => ( ( "." IDENTIFIER ( "(" ArgumentList? ")" )? ) | DimentionsExpression )*
;

PrimaryNewArray:
    class=ArrayCreationExpression extension=ExpressionExtends
//    => ( ( "." IDENTIFIER ( "(" ArgumentList? ")" )? ) | DimentionsExpression )*
;

ExpressionExtends:
    => ( ( "." field=IDENTIFIER ( "(" args=ArgumentList? ")" )? ) | dimention=DimentionsExpression )*
;

ClassInstanceCreationExpression:
    NEW class=ClassType "(" args=ArgumentList? ")"
;

ArgumentList:
    expression+=Expression (=> "," expressions+=Expression )*
;

ArrayCreationExpression:
    NEW type=( PrimitiveType | TypeName )
    => expressions+=DimentionsExpression+ dimentions+=ArrayType*
;

DimentionsExpression:
    "[" expression=Expression "]"
;

ArrayAccess:
    object=PrimaryNoNewArray index=DimentionsExpression
//    ( => ExpressionName | PrimaryNoNewArray ) DimentionsExpression
;

// ===============================================
// ===                 Tokens                  ===
// ===============================================

PackageName:
    IDENTIFIER
    (=> "." IDENTIFIER )*
;

TypeName:
    IDENTIFIER
    (=> "." IDENTIFIER )*
;

SimpleTypeName:
    IDENTIFIER
;

ExpressionName:
    IDENTIFIER
    (=> "." IDENTIFIER )*
;

MethodName:
    IDENTIFIER
    (=> "." IDENTIFIER )*
;

AmbiguousName:
    IDENTIFIER
    ( => "." IDENTIFIER )*
;

Literal returns Literal:
    type=( IntegerLiteral | FloatingLiteral | BooleanLiteral | CHARACTER_LITERAL | STRING_LITERAL | NULL )
;

IntegerLiteral:
    ( DECIMAL_NUMERAL | HEX_NUMERAL | OCTAL_NUMERAL ) ( "l" | "L" )?
;

FloatingLiteral:
    ( ( DECIMAL_NUMERAL "." DECIMAL_NUMERAL* ) | ( "." DECIMAL_NUMERAL ) | DECIMAL_NUMERAL )
    FLOAT_PART_COMPLEMENT
;

BooleanLiteral:
    TRUE | FALSE
;

// ===============================================
// ===                Terminals                ===
// ===============================================

terminal DOC_COMMENT:
    "/*" -> "*/"
;

terminal SL_COMMENT:
    "//" !( "\n" | "\r" )* ( "\r"? "\n" )?
;

terminal WS:
    ( " " | "\t" | "\r" | "\n" )+
;

terminal ABSTRACT:
    "abstract"
;

terminal AMPERSAND:
    "ampersand"
;

terminal BOOLEAN:
    "boolean"
;

terminal BREAK:
    "break"
;

terminal BYTE:
    "byte"
;

terminal CASE:
    "case"
;

terminal CATCH:
    "catch"
;

terminal CHAR:
    "char"
;

terminal CLASS:
    "class"
;

terminal CONST:
    "const"
;

terminal CONTINUE:
    "continue"
;

terminal DEFAULT:
    "default"
;

terminal DO:
    "do"
;

terminal DOUBLE:
    "double"
;

terminal ELSE:
    "else"
;

terminal EXTENDS:
    "extends"
;

terminal FALSE:
    "false"
;

terminal FINAL:
    "final"
;

terminal FINALLY:
    "finally"
;

terminal FLOAT:
    "float"
;

terminal FOR:
    "for"
;

terminal GOTO:
    "goto"
;

terminal IF:
    "if"
;

terminal IMPLEMENTS:
    "implements"
;

terminal IMPORT:
    "import"
;

terminal INSTANCEOF:
    "instanceof"
;

terminal INT:
    "int"
;

terminal INTERFACE:
    "interface"
;

terminal LONG:
    "long"
;

terminal NATIVE:
    "native"
;

terminal NEW:
    "new"
;

terminal NULL:
    "null"
;

terminal PACKAGE:
    "package"
;

terminal PRIVATE:
    "private"
;

terminal PROTECTED:
    "protected";

terminal PUBLIC:
    "public"
;

terminal RETURN:
    "return"
;

terminal SHORT:
    "short"
;

terminal STATIC:
    "static"
;

terminal SUPER:
    "super"
;

terminal SWITCH:
    "switch"
;

terminal SYNCHRONIZED:
    "synchronized"
;

terminal THIS:
    "this"
;

terminal THREADSAFE:
    "threadsafe"
;

terminal THROW:
    "throw"
;

terminal THROWS:
    "throws"
;

terminal TRANSIENT:
    "transient"
;

terminal TRY:
    "try"
;

terminal TRUE:
    "true"
;

terminal VOID:
    "void"
;

terminal VOLATILE:
    "volatile"
;

terminal WHILE:
    "while"
;

terminal IDENTIFIER:
    ( "a".."z" | "A".."Z" |"_" |"$" )
    ( "a".."z" | "A".."Z" |"_" |"$" | "0".."9" )*
;

terminal DECIMAL_NUMERAL:
    "0" | ( NON_ZERO_DIGIT DIGIT* )
;

terminal fragment DIGIT:
    "0" | NON_ZERO_DIGIT
;

terminal fragment NON_ZERO_DIGIT:
    "1" .. "9"
;

terminal HEX_NUMERAL:
    ( "0x" | "0X" ) HEX_DIGIT+
;

terminal fragment HEX_DIGIT:
    "0" .. "9" | "a" .. "f" | "A" .. "F"
;

terminal OCTAL_NUMERAL:
    "0" OCTAL_DIGIT+
;

terminal fragment OCTAL_DIGIT:
    "0" .. "7"
;

terminal FLOAT_PART_COMPLEMENT:
    EXPONENT_PART? FLOAT_TYPE_SUFFIX?
;

terminal fragment EXPONENT_PART:
    EXPONENT_INDICATOR SIGNED_INTEGER
;

terminal fragment EXPONENT_INDICATOR:
    "e" | "E"
;

terminal fragment SIGNED_INTEGER:
    ( "+" | "-" ) DIGIT+
;

terminal fragment FLOAT_TYPE_SUFFIX:
    "f" | "F" | "d" | "D"
;

terminal CHARACTER_LITERAL:
    "'" . | !( "\\" | "'" ) "'"
;

terminal STRING_LITERAL:
    '"' ( "\\" . | !( "\\" | '"' ) )* '"'
;